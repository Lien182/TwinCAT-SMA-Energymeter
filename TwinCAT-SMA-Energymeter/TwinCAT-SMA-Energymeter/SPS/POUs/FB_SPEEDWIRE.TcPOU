<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_SPEEDWIRE" Id="{301027ce-e72f-4b06-87fa-320a9ed7aa71}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SPEEDWIRE
VAR_INPUT
	bEnable					: BOOL 	:= TRUE;
	nLocalPort				: UDINT := 9522;
	sLocalHost				: T_IPv4Addr := '192.168.0.227';(* Adapt local IP address to your device *)
	nRemotePort				: UDINT := 9522;
	sMulticastAddr			: T_IPv4Addr := '239.12.255.254';
	
	EL3443		: MDP5001_341_A3044239;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	fbUdpCreate 			: FB_SocketUdpCreate;
	fbSocketAddMulticast 	: FB_SocketUdpAddMulticastAddress;
	fbSocketDropMulticast 	: FB_SocketUdpDropMulticastAddress;
	fbSocketSend  			: FB_SocketUdpSendTo;
	fbSocketReceive 		: FB_SocketUdpReceiveFrom;
	fbSocketClose			: FB_SocketClose;
	fbCloseAll				: FB_SocketCloseAll;
	bInitialize				: BOOL := TRUE;
	bUdpCreated				: BOOL;
	bMulticastAdded			: BOOL; 
	
	nCounterIn 				: UDINT;
	nCounterOut 			: UDINT;
	nState 					: INT;		
	
	fbEL3443_2_OBIS			: EL3443_OBIS;
	
	send_ton 				: TON;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bInitialize = TRUE THEN
(* On PLC reset or program download close all opened/created sockets *)
	fbCloseAll( bExecute:= bInitialize );
	IF fbCloseAll.bBusy OR fbCloseAll.bError THEN
	//	RETURN;
	END_IF
	bInitialize := FALSE;
END_IF






CASE nState OF
0:
	IF bEnable THEN
		bUdpCreated := FALSE;
		bMulticastAdded := FALSE;
		nState := 1;
	END_IF
1:
	fbUdpCreate( bExecute := FALSE );
	fbUdpCreate( nLocalPort := nLocalPort, sLocalHost := sLocalHost, bExecute := TRUE);
	nState := 2;
2:
	fbUdpCreate( bExecute := FALSE );
	IF NOT fbUdpCreate.bBusy THEN
		IF NOT fbUdpCreate.bError THEN
			bUdpCreated := TRUE;
			fbSocketAddMulticast( bExecute := FALSE );
			fbSocketAddMulticast( hSocket := fbUdpCreate.hSocket, sMulticastAddr := sMulticastAddr, bExecute := TRUE );
			nState := 3;
		ELSE
			nState := 100;
		END_IF
	END_IF
3:
	fbSocketAddMulticast( bExecute := FALSE );
	IF NOT fbSocketAddMulticast.bBusy THEN
		IF NOT fbSocketAddMulticast.bError THEN
			bMulticastAdded := TRUE;
			nState := 4;
		ELSE
			nState := 100;
		END_IF
	END_IF
	
4:
	IF bEnable THEN
		nCounterOut := nCounterOut + 1;
		fbSocketSend( bExecute := FALSE );
		fbEL3443_2_OBIS(EL3443 := EL3443);
		fbSocketSend( nRemotePort := nRemotePort, sRemoteHost := sMulticastAddr, hSocket := fbUdpCreate.hSocket, cbLen:= fbEL3443_2_OBIS.GetDataLength(), pSrc := fbEL3443_2_OBIS.GetData(), bExecute := TRUE );
		nState := 5;
	ELSE
		nState := 10;
	END_IF
5:
	fbSocketSend( bExecute := FALSE );
	IF NOT fbSocketSend.bBusy THEN
		IF NOT fbSocketSend.bError THEN
			nState := 6;
		ELSE
 			nState := 100;
		END_IF
	END_IF
6:	
	fbSocketReceive( bExecute := FALSE );
	IF NOT fbSocketReceive.bBusy THEN
		IF NOT fbSocketReceive.bError THEN
			nState:= 7;
			
		ELSE
			nState := 100;
		END_IF
	END_IF

7:	send_ton(IN := TRUE, PT:= T#200MS);
	IF send_ton.Q THEN
		nState:= 4;
		send_ton(IN:=FALSE);
	END_IF
	
10:
	fbSocketDropMulticast( bExecute := FALSE );
	fbSocketDropMulticast( hSocket := fbUdpCreate.hSocket, sMulticastAddr := sMulticastAddr, bExecute := TRUE );
	nState := 11;
11:	
	fbSocketDropMulticast( bExecute := FALSE );
	IF NOT fbSocketDropMulticast.bBusy THEN
		bMulticastAdded := FALSE;
		IF NOT fbSocketDropMulticast.bError THEN
			nState := 20;
		ELSE
			nState := 100;
		END_IF
	END_IF

20:	
	fbSocketClose( bExecute := FALSE );
	fbSocketClose( hSocket := fbUdpCreate.hSocket, bExecute := TRUE );
	nState := 21;
21:	
	fbSocketClose( bExecute := FALSE );
	IF NOT fbSocketClose.bBusy THEN
		bUdpCreated := FALSE;
		IF NOT fbSocketClose.bError THEN
			nState := 0;	
		ELSE
			nState := 100;	
		END_IF
	END_IF
	
100:
	IF bMulticastAdded THEN
		nState := 10;
	ELSIF bUdpCreated THEN
		nState := 20;
	ELSE
		nState := 0;
	END_IF
	
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="FB_SPEEDWIRE">
      <LineId Id="3" Count="122" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>